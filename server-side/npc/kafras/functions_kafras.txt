//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Dastgir
//= Copyright (C)  Haru
//= Copyright (C)  rAthena Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  Joseph
//= Copyright (C)  eAthena Dev Team
//= Copyright (C)  Daegaladh
//= Copyright (C)  brianluau
//= Copyright (C)  Kisuka
//= Copyright (C)  Evera
//= Copyright (C)  erKURITA
//= Copyright (C)  Silentdragon
//= Copyright (C)  Nexon
//= Copyright (C)  L0ne_W0lf
//= Copyright (C)  kobra_k88
//= Copyright (C)  Lupu
//= Copyright (C)  Syrus22
//= Copyright (C)  Darkchild
//= Copyright (C)  Darlskies
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Kafra Functions
//================= Description ===========================================
//= These functions handle save, storage, cart rental, teleport, and Free
//= Teleport/Warp/Rent Cart options for all Kafra NPCs.
//================= Current Version =======================================
//= 6.9
//=========================================================================

//== Main Function =========================================
// Arguments:
// - 0: Used to determine which welcome message to show.
// - 1: Used to determine which menu to display.
// - 2: Used to determine if the info menu is shown in F_KafInfo.
// - 3: Cost to use Storage
// - 4: Cost to Rent a Pushcart
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	// Display Kafra Welcome Message
	switch (getarg(0)) {
		default:
		case 0:
			// Default message (obsolete)
			mes "[Kafra Employee]";
			mes "Welcome to the";
			mes "Kafra Corporation.";
			mes "The Kafra services";
			mes "are always on your side.";
			mes "How may I assist you?";
			break;
		case 1:
			// Niflheim specific message
			mes "[Kafra Employee]";
			mes "^666666W-weeeelc-c-come";
			mes "to th-the K-kaaafrrrra";
			mes "C-coorpoor-r-ratioooonn...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			@GID = getcharid(CHAR_ID_GUILD);
			mes "[Kafra Employee]";
			mes "Welcome. ^ff0000" + getguildinfo(GUILDINFO_NAME, @GID) + "^000000 Member.";
			mes "The Kafra Coporation will stay with you wherever you go.";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[Kafra Employee]";
			mes "So, have you come from a faraway land to study our culture, or are you just sightseeing?";
			mes "In either case, why not stay awhile?";
			mes "The air is eternally heavy with the";
			mes "scent of pleasant wildflowers.";
			break;
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[Kafra Employee]";
			mes "With our many Kafra";
			mes "service locations, you're never";
			mes "far from home.";
			break;
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	next;
	while (true) {
		deletearray @K_Menu0$;
		if (getarg(0) == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"Use Storage","Use Guild Storage","Rent a Pushcart","Use Teleport Service","Cancel";
		} else {
			switch (getarg(1)) {
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"Save","Use Storage","Cancel"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"Use Storage","Cancel"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"Save","Use Storage","Rent a Pushcart","Check Other Information","Cancel"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"Use Storage","Rent a Pushcart","Check Other Information","Cancel"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"Use Storage","Check Other Information","Cancel"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"Save","Use Storage","Rent a Pushcart","Cancel"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"Save","Use Storage","Check Other Information","Cancel"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"Use Storage","Rent a Pushcart","Use Teleport Service","Check Other Information","Cancel"; break;
				// Storage, Save, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"Use Storage","Save","Rent a Pushcart","Cancel"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"Save","Use Storage","Use Teleport Service","Rent a Pushcart","Check Other Information","Cancel"; break;
			}
		}
		.@menu$ = "";
		for (.@i = 0; .@i < getarraysize(@K_Menu0$); ++.@i)
			.@menu$ += @K_Menu0$[.@i] + ":";
		.@j = select(.@menu$) - 1;
		if (@K_Menu0$[.@j] == "Save") {
			return;
		} else if (@K_Menu0$[.@j] == "Use Storage") {
			// Do not charge for Guild Storage
			if(getarg(0) == 2)
				callfunc "F_KafStor",2,0,0,"[Kafra Employee]";
			else
				callfunc "F_KafStor",0,getarg(3),getarg(0),"[Kafra Employee]";
			next;
		} else if (@K_Menu0$[.@j] == "Use Teleport Service") {
			// Display Einbroch "No Teleport Service" notice.
			if (getarg(1) != 4) {
				callfunc "F_KafTele",getarg(0),"[Kafra Employee]";
			} else {
				mes "[Kafra Employee]";
				mes "Por conta do ^FF0000Limited";
				mes "Acordo de transporte^000000, a";
				mes "Kafra nao pode";
				mes "ofertar o servico de teleporte";
				mes "na Republica Schwaltzvalt.";
				next;
				mes "[Kafra Employee]";
				mes "Nos pedimos que voce";
				mes "use o servico de transporte aereo";
				mes "Obrigada pela";
				mes "compreensao and cooperacao.";
			}
			next;
		} else if (@K_Menu0$[.@j] == "Rent a Pushcart") {
			if(callfunc("F_KafCart",getarg(0),getarg(4),"[Kafra Employee]") == 1)
				next;
		} else if (@K_Menu0$[.@j] == "Check Other Information") {
			callfunc "F_KafInfo",getarg(2);
			next;
		} else if (@K_Menu0$[.@j] == "Cancel"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		} else if (@K_Menu0$[.@j] == "Use Guild Storage") {
			callfunc "F_KafStor",1,0,0,"[Kafra Employee]";
			next;
		}
	}
}

//== Storage Function ======================================
// Arguments:
// - 0: Specifies the type of storage that has been requested:
//      0 = Regular storage
//      1 = Guild storage
//      2 = Regular storage, in a castle (free of charge)
// - 1: Cost to use storage.
// - 2: The type of NPC this is being called from. Notable values:
//      0,2-5 = Regular Kafra
//      1 = Niflheim Kafra (special messages)
//      6 = Zonda/Cool Event Staff (no RESRVPTS)
// - 3: NPC title to display.
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if (getarg(0) == 1) {
		if (guildopenstorage()) {
			mes getarg(3);
			mes "Desculpa mas outra membro da sua Guilda esta usando o deposito";
			mes "nesse momento.  Aguarde ela finalizar.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if (basicskillcheck() && getskilllv(NV_BASIC) < 6 && getskilllv(SU_BASIC_SKILL) == 0) {
		mes getarg(3);
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-s-ssoooorry,";
			if (Class == Job_Summoner) {
				mes "v-voce p-precisa";
				mes "N-nova habilidadeee b-basica...^000000";
			} else {
				mes "v-voce e u-ummm";
				mes "Noovatoooo... p-precisaaa";
				mes "H-habilidadeee basicaaa l-level 6...^000000";
			}
			return;
		}
		mes "I'm sorry, but you";
		if (Class == Job_Summoner) {
			mes "need the New Basic Skill to";
		} else {
			mes "need the Novice's";
			mes "Basic Skill Level 6 to";
		}
		mes "use the Storage Service.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if (getarg(0) != 2) {
		// Consume "Free Ticket for Kafra Storage" if available.
		if (countitem(Cargo_Free_Ticket)) {
			delitem Cargo_Free_Ticket, 1;
		} else {
			if (Zeny < getarg(1)) {
				mes getarg(3);
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "M-mais z-zeny...!";
					mes "P-preciso de 150... zeny...";
					mes "Ergh!^000000";
					return;
				}
				// Standard Message
				mes "Me desculpe mas voce nao";
				mes "tem zeny suficiente para usar";
				mes "O servico de deposito. Our";
				mes "A taxa do servico e "+getarg(1)+" zeny.";
				return;
			}
			Zeny -= getarg(1);
			if (getarg(2) != 6)
				RESRVPTS += getarg(1) / 5;
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		if (getarg(2) == 1) {
			// Niflheim specific Message
			percentheal 0,-10;
			mes getarg(3);
			for (.@i = 0; .@i < 5; ++.@i)
				mes "^666666Obrigado.. por... utilizar...^000000";
		} else if (getarg(2) == 6) {
			// Zonda
			mes getarg(3);
			mes "Deixe eu abrir o seu deposito";
			mes "pessoal agora mesmo.";
			mes "Obrigado pelo suporte";
			mes "e por usar os nossos";
			mes "servicos. Tenha um bom dia~";
		} else {
			// Normal message
			mes getarg(3);
			mes "Deixe eu abrir";
			mes "seu deposito para voce.";
			mes "Obrigado por usar o servico";
			mes "da Corporacao Kafra.";
		}
	}
	callfunc("F_CheckKafCode"); //check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//== Teleport Function =====================================
// Arguments:
// - 1: NPC title to display.
// - 2: The type of NPC this is being called from. Notable values:
//      2 = Guild Castle Kafra (don't consume tickets)
//      6 = Zonda/Cool Event Staff (no RESRVPTS)
function	script	F_KafTele	{
	mes getarg(1);
	mes "Por favor";
	mes "escolha seu destino.";
	next;
	.@menu$ = "";
	for (.@i = 0; .@i <= getarraysize(@wrpD$); ++.@i)
		.@menu$ += @wrpC$[.@i] + ":";
	.@j = select(.@menu$) - 1;
	if (@wrpC$[.@j] == "Cancel")
		return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if (countitem(Warp_Free_Ticket) > 0 && getarg(0) != 2) {
		delitem Warp_Free_Ticket, 1;
	} else {
		if (Zeny < @wrpP[.@j]) {
			mes getarg(1);
			mes "ME desculpe mas voce nao tem";
			mes "zeny  o suficiente para usar o servico de";
			mes "teleporte. A taxa de teleporte e ";
			mes ""+@wrpD$[.@j]+" is "+@wrpP[.@j]+" zeny.";
			close2;
			cutin "", 255;
			end;
		}
		Zeny -= @wrpP[.@j];
		if (getarg(0) != 6)
			RESRVPTS += @wrpP[.@j] / 16;
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") { if (RENEWAL) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mjolnir Dead Pit") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Orc Dungeon") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	else if (@wrpD$[.@j] == "Veins") warp "veins",205,101;
	else if (@wrpD$[.@j] == "Rachel") warp "rachel",115,125;
	end;
}

//== Cart Function =========================================
// Arguments:
// - 0: The type of NPC this is being called from. Notable values:
//      0,1,3-5 = Regular Kafra
//      2 = Guild Castle Kafra (don't consume tickets)
//      6 = Zonda/Cool Event Staff (no RESRVPTS)
// - 1: Cost to rent a cart.
// - 2: NPC title to display.
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if (BaseClass != Job_Merchant) {
		mes getarg(2);
		mes "Perdao, mas o";
		mes "o servico de aluguel de carrinhos";
		mes "so esta disponivel para Comerciantes,";
		mes "Ferreiros, Mestres de Forja,";
		mes "Alquimista and Bioalquimistas.";
		return 1;
	}
	if (getskilllv(MC_PUSHCART) == 0) {
		mes getarg(2);
		mes "Voce so pode alugar um carrinho depois de";
		mes "aprender a habilidade de carrinho.";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	if (checkcart() == 1) {
		mes getarg(2);
		mes "Voce ja tem";
		mes "um carrinho equipado.";
		mes "Infelizmente, nao podemos";
		mes "alugar mais de um para";
		mes "cada cliente.";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if (countitem(Cart_Free_Ticket) > 0 && getarg(0) != 2) {
		delitem Cart_Free_Ticket, 1;
	} else {
		mes getarg(2);
		mes "A taxa de ";
		mes "aluguel e "+getarg(1)+" zeny. Voce";
		mes "gostaria de alugar um carrinho?";
		next;
		if (select("Alugar um carrinho.", "Cancel") == 2)
			return 0;
		if (Zeny < getarg(1)) {
			mes getarg(2);
			mes "Desculpe, mas voce nao";
			mes "tem zeny o suficiente";
			mes "para pagar pela taxa";
			mes "de aluguel de "+getarg(1)+" zeny.";
			return 1;
		}
		Zeny -= getarg(1);
		if (getarg(0) != 6)
			RESRVPTS += 48;
	}
	setcart;
	return 1;
}

//== Special Reserve Points Function =======================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//.@block = 1;
	setarray .@m$, "Check Special Reserve Points.","Storage Password Service","Kafra Employee Locations","Cancel";
	if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight: remove Storage Password and Kafra Employee Locations
	else if (.@block) deletearray .@m$[1],1; // Remove Storage Password
	while (true) {
		.@j = select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "Check Special Reserve Points.") {
			mes "[Kafra Employee]";
			mes "Let's see...";
			mes strcharinfo(PC_NAME) + "...";
			mes "Ah, you have a total of";
			mes RESRVPTS+ " Special Reserve Points.";
			next;
			mes "[Kafra Employee]";
			mes "You can exchange your";
			mes "Special Reserve Points for";
			mes "rewards at the Kafra Main Office in Al De Baran. Please use our";
			mes "convenient services to see the benefits of our rewards program.";
			next;
			if(getarg(0) == 1)
				return;
		} else if (.@m$[.@j] == "Kafra Employee Locations") {
			for (.@i = 0; .@i <= 3; ++.@i)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (.@i = 0; .@i <= 3; ++.@i)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		} else if (.@m$[.@j] == "Storage Password Service") {
			callfunc("F_SetKafCode","[Kafra Employee]","Kafra Services");
		} else {
			return;
		}
	}
}

//== End Function ==========================================
// Arguments:
// - 0: used to determine what message to display.
// - 1: used to determine if save message is displayed.
// - 2: used to display the name of the area you're saving in.
function	script	F_KafEnd	{
	mes "[Kafra Employee]";
	if (getarg(1) == 1) {
		// Save
		mes "Seu Ponto de Recuperacao";
		mes "foi salvo aqui";
		mes getarg(2)+".";
		mes "obrigado por utilizar";
		mes "os Servicos Kafra.";
	} else if (getarg(0) == 0 || getarg(0) == 5) {
		// Generic End
		mes "Nos, da Corporacao Kafra,";
		mes "estamos sempre felizes de oferecer o melhor servico. Obrigado";
	} else if (getarg(0) == 1) {
		// Niflheim End
		percentheal 0,-25;
		mes "^666666Kaffffra n-nunca";
		mes "morraaaaaa. On...";
		mes "Do s-seuu laaadooo~^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//== Check Storage Password Function =======================
function	script	F_CheckKafCode	{
	if (!#kafra_code)
		return;
	mes "Digite a senha do deposito:";
	@code_ = 0;
	input @code_;
	if (@code_ != #kafra_code) {
		dispbottom "Senha de deposito incorreta.";
		close2;
		cutin "",255;
		end;
	}
	@kafcode_try = 0;
	@code_ = 0;
	return;
}

//== Set / Change / Clear Storage Password Function ========
// Arguments:
// - 0: NPC Name
// - 1: Company Name
function	script	F_SetKafCode	{
	mes getarg(0);
	if (#kafra_code) {
		mes "Seu deposito esta protegido com uma senha. O que voce gostaria de fazer?";
		next;
		switch (select("Alterar a senha antiga -> 5000z", "Remover a senha de deposito-> 1000z", "Cancelar")) {
			case 1:
				mes getarg(0);
				mes "Por favor digite sua ^0000FFold senha^000000.";
				@code = callfunc("F_EntKafCode");
				if (!@code || @code != #kafra_code) {
					mes "Senha incorreta. Nao foi possivel definir uma nova.";
					emotion e_hmm;
					break;
				}
				next;
				callsub S_SET,getarg(0),getarg(1);
				break;
			case 2:
				mes getarg(0);
				mes "Por favor digite sua senha antes de remove-la.";
				@code = callfunc("F_EntKafCode");
				if (!@code) {
					mes "A senha nao pode ser removida.";
					emotion e_hmm;
					break;
				}
				next;
				mes getarg(0);
				if (Zeny < 1000) {
					mes "Voce nao tem dinheiro o suficiente.";
					emotion e_cash;
					break;
				}
				Zeny -= 1000;
				if (@code == #kafra_code) {
					#kafra_code = 0;
					mes "Voce conseguiu remover sua senha de deposito.";
					mes "Obrigado por utilizar "+getarg(1)+".";
					emotion e_thx;
				} else {
					mes "Senha incorreta. Nao iremos devolver seus 1000z.";
					mes "Por favor, da proxima vez digite a senha correta.";
					emotion e_sry;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes getarg(1)+" lhe apresento um novo servico:";
		mes "Servico Adicional de protecao do deposito.";
		next;
		switch (select("Definir uma nova senha -> 5000z", "Cancelar")) {
			case 1: callsub S_SET,getarg(0),getarg(1); break;
			case 2: break;
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes getarg(0);
	mes "Agora digite sua ^FF0000nova senha^000000 para proteger seu deposito de ladroes.";
	@code = callfunc("F_EntKafCode");
	if (!@code) {
		mes "Sua senha nao pode ser alterada.";
		emotion e_hmm;
		return;
	}
	next;
	mes getarg(0);
	if (Zeny < 5000) {
		mes "Voce nao tem dinheiro para isso.";
		emotion e_cash;
		return;
	}
	Zeny -= 5000;
	#kafra_code = @code;
	mes "Voce habilitou a protecao por senha.";
	mes "Obrigado por utilizar o "+getarg(1)+".";
	emotion e_thx;
	return;
}

//== Basic Password Validation Function ====================
function	script	F_EntKafCode	{
	mes "Digite um numero 1000~10000000:";
	@code_ = 0;
	++@kafcode_try;
	if (@kafcode_try > 10) {
		@kafcode_try = 0;
		logmes "Hack: Tentou usar a senha de protecao.";
	}
	if (input(@code_) == 1) {
		mes "Voce nao pode usar uma senha tao grande.";
		return 0;
	}
	if (@code_ < 1000) {
		mes "Voce nao pode usar uma senha tao pequena.";
		return 0;
	}
	return @code_;
}

//== Warp Points / View Points Function ====================
function	script	F_KafSet	{
	deletearray @wrpC$;
	deletearray @wrpD$;
	deletearray @wrpP$;
	deletearray @viewpX;
	deletearray @viewpY;
	if (strnpcinfo(NPC_MAP) == "prontera") {
		setarray @wrpD$, "Izlude", "Geffen", "Payon", "Morroc", "Orc Dungeon", "Alberta";
		setarray @wrpP, 600, 1200, 1200, 1200, 1200, 1800;
		setarray @viewpX, 151, 29, 282, 152;
		setarray @viewpY, 29, 207, 200, 326;
	} else if (strnpcinfo(NPC_MAP) == "alberta") {
		setarray @wrpP, 1200, 1800, 1800;
		setarray @wrpD$, "Payon", "Morroc", "Prontera";
		setarray @viewpX, 28, 113, 0, 0;
		setarray @viewpY, 229, 60, 0, 0;
	} else if (strnpcinfo(NPC_MAP) == "aldebaran") {
		setarray @wrpP, 1200, 1200, 1800, 1700;
		setarray @wrpD$, "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	} else if (strnpcinfo(NPC_MAP) == "comodo" ) {
		setarray @wrpD$, "Morroc", "Comodo Pharos Beacon", "Umbala";
		setarray @wrpP, 1800, 1200, 1800;
	} else if (strnpcinfo(NPC_MAP) == "cmd_fild07") {
		setarray @wrpD$, "Comodo", "Morroc";
		setarray @wrpP, 1200, 1200;
	} else if (strnpcinfo(NPC_MAP) == "geffen") {
		setarray @wrpD$, "Prontera", "Al De Baran", "Orc Dungeon", "Mjolnir Dead Pit";
		setarray @wrpP, 1200, 1200, 1200, 1700;
		setarray @viewpX, 120, 203;
		setarray @viewpY, 62, 123;
	} else if (strnpcinfo(NPC_MAP) == "izlude") {
		setarray @wrpD$, "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP, 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(NPC_MAP) == "morocc") {
		setarray @wrpD$, "Prontera", "Payon", "Alberta", "Comodo", "Comodo Pharos Beacon";
		setarray @wrpP, 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX, 156, 163, 28, 292;
		setarray @viewpY, 97, 260, 167, 211;
	} else if (strnpcinfo(NPC_MAP) == "umbala") {
		setarray @wrpD$, "Comodo";
		setarray @wrpP, 1800;
	} else if (strnpcinfo(NPC_MAP) == "payon") {
		setarray @wrpD$, "Prontera", "Alberta", "Morroc";
		setarray @wrpP, 1200, 1200, 1200;
	} else if (strnpcinfo(NPC_MAP) == "yuno") {
		setarray @wrpD$, "Al De Baran";
		setarray @wrpP, 1200;
		setarray @viewpX, 328, 278, 153, 0;
		setarray @viewpY, 108, 221, 187, 0;
	} else if (strnpcinfo(NPC_MAP) == "job3_rune01") {
		setarray @wrpD$, "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP, 600, 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(NPC_MAP) == "rachel") {
		setarray @wrpD$, "Veins";
		setarray @wrpP, 2200;
	} else if (strnpcinfo(NPC_MAP) == "veins") {
		setarray @wrpD$, "Rachel";
		setarray @wrpP, 2200;
	}
	for (.@i = 0; .@i < getarraysize(@wrpD$); ++.@i)
		@wrpC$[.@i] = @wrpD$[.@i]+" -> "+@wrpP[.@i];
	@wrpC$[getarraysize(@wrpC$)] = "Cancel";
	return;
}
